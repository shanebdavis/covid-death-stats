import &StandardImport, &Lib


formatNumber = (p) ->
  if isInfinite p
    :Infinity
  else
    switch
    when p >= 99999999 then "#{Math.round(p / 1000000)}m"
    when p >= 999999 then "#{(p / 1000000).toFixed 1}m"
    when p >= 99999 then "#{Math.round(p / 1000)}k"
    when p >= 9999 then "#{(p / 1000).toFixed 1}k"
    when p >= 999 then commaize Math.round p
    when p >= 100 then Math.round p
    when float32Eq (p | 0), p then p | 0
    when p >= 10 then p.toFixed 1
    else p.toFixed 2

formatProbability = (p) ->
  if p < .001001
    "1:#{formatNumber 1/p}"
  else
    100 * p
    .toFixed
      switch
      when p < .010001 then 3
      when p < .100001 then 2
      else 1
    + :%

class Bar extends ViewStateComponent

  @propFields :value :unit :label :maxValue :probabilityMode

  @stateFields
    mouseIn: null

  setMouseIn: ({target, location}) ->
    @mouseIn = point
      if location.x > target.currentSize.x / 2 then 1 else 0
      bound 0, location.y / target.currentSize.y, 1

  render: ->
    @ extract value
    if isInfinite value
      value = @maxValue

    Element
      key: @label
      size:
        ww: 1
        hh: min 1, scale = value / @maxValue
      draw: #d
      on:
        mouseIn: @setMouseIn

        mouseOut: ({target, location}) ->
          @setMouseIn {target, location}
          timeout 5 -> @clearMouseIn()

      animators:
        size: toFrom: w: 0 hh: scale
        opacity: toFrom: 0

      Element
        :clip
        if @mouseIn?
          Element
            location: ps: @mouseIn
            axis: .5
            size: ({x, y}) -> point 1.41 * 2 * max x, y
            animators: size:
              d: 1
              toFrom: 0
            draw:
              :circle
              #ca00ca

      TextElement
        TextStyles.tinyBoldText
        :childrenSize
        axis: :bottomCenter
        location: h: -@gridSize / 2, xw: .5
        color: TextPalette.black.secondary
        text:
          if @value
            switch @probabilityMode
            when :relative :count then formatNumber @value
            when :probability then formatProbability @value
            else
              if isInfinite @value
                :infinity
              else
                @value + @unit
          else '' + @value

      TextElement
        TextStyles.tinyBoldText
        :childrenSize
        :textCenterCenter
        axis: :topCenter
        location: yh: 1 y: @gridSize * 2 xw: .5
        color: TextPalette.black.secondary
        text: @label
