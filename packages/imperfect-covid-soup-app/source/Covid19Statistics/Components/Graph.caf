import &StandardImport, &Lib, {} &Bar, &Button

sum = (list) ->
  reduce a, b from list inject 0
    if a? and b?
      a + b
    else null

class Graph extends ViewStateComponent

  @subscriptions
    :covidData
    :graphState

  @stateFields
    probabilityMode: :relative
    selectedData: null
    showOptionsFor: null
    exampleData: null

  @propFields :animationOn

  applyScale = (scale, obj) ->
    if scale? && scale != 1
      object v, k in obj with v * scale
    else obj

  createSelectedDataEntry = (covidDataRecord, region, age, intentData, mechanismData, scale) ->
    merge
      covidDataRecord
      nonCovid19Deaths: covidDataRecord.totalDeaths - covidDataRecord.covid19Deaths
      applyScale scale, {totalDeaths: totalDeaths2019, accident: accidentDeaths2019, homicide: homicides2019, suicide: suicides2019} = intentData?[region]?[age] ? {}
      applyScale scale, {drowning: drownings2019, motorVehicle: carAccidents2019, disease: diseaseDeaths2019, firearm: firearmDeaths2019, poisoning: poisonings2019, suffocation: suffocationDeaths2019, fall: deathFromFalls2019} = mechanismData?[region]?[age] ? {}

  preprocessState: (newState) ->
    newState.covidData extract? covidData, intentData, mechanismData, times, ages
    newState.graphState extract? region, time, age, graphType
    [aRegion] = region
    [aTime] = time
    [anAge] = age

    [axis] = graphType

    axisKeys = if axis == :time
      if aTime == "All Time"
        array v from times when !/^All|^202\d$/.test v
      else time
    else
      if anAge == "All Ages"
        array v from ages when !/^All/.test v
      else age

    merge
      newState
      exampleData: createSelectedDataEntry
        covidData.UnitedStates["All Time"]["All Ages"]
        :UnitedStates
        "All Ages"
        intentData
        mechanismData

      selectedData:
        unless covidData
          {}
        else
          if axis == :time
            object aTime from axisKeys
              if region.length > 1
                array aRegion in region
                  createSelectedDataEntry
                    covidData[aRegion][aTime][anAge]
                    aRegion
                    anAge
                    intentData
                    mechanismData
                    1/12

              else
                array anAge in age
                  createSelectedDataEntry
                    covidData[aRegion][aTime][anAge]
                    aRegion
                    anAge
                    intentData
                    mechanismData
                    1/12

          else
            object anAge from axisKeys
              if region.length > 1
                array aRegion in region
                  createSelectedDataEntry
                    covidData[aRegion][aTime][anAge]
                    aRegion
                    anAge
                    intentData
                    mechanismData

              else
                array aTime in time
                  createSelectedDataEntry
                    covidData[aRegion][aTime][anAge]
                    aRegion
                    anAge
                    intentData
                    mechanismData

  valueCalc: (row, numerator, denominator) ->
    n = if numerator == "1" then 1 else row[numerator]
    d = if denominator == "1" then 1 else row[denominator]
    if n? && d?
      n / d
    else null

  @getter
    relative: -> @probabilityMode == :relative
    unit: ->
      switch @probabilityMode
      when :relative then :x
      when :probability then :%
      else ''

    data: ->
      @graphState extract layout, numerator, denominator
      array rows, label in @selectedData
        [firstRow] = rows
        {}
          label:
            if m = label.match /(^\d+)-(\d+)$/
              [_, year, month] = m
              "#{months[month|0]}\n#{year}"
            else
              label
              .replace /Under /g 'Under\n'
              .replace /[ ]years/g '\nyears'

          values =
            if numerator.length > 1
              array n in numerator with @valueCalc firstRow, n, denominator[0]
            else if denominator.length > 1
              array d in denominator with @valueCalc firstRow, numerator[0], d
            else
              array row in rows with @valueCalc row, numerator[0], denominator[0]

          value: log
            if layout == :stacked
              sum log values
            else
              Math.max values...

    maxValue: ->
      base =
        reduce a, {value, label}, k from @data inject 0 when !/All|Jan\n2020|Feb\n2020/.test label
          max a, switch
          when isInfinite(value) then 0
          else value

      find v from maxValues when base < v

  renderLabledSection: ({label, items}) ->
    Element
      :flow
      :parentWidthChildrenHeight
      key: label
      animators: opacity: toFrom: 0
      childrenMargins: @gridSize / 2
      array item in items
        Button item

  getOptions: (optionsType) ->
    switch optionsType
    when :graphType then :time :age
    when :numerator :denominator then compactFlattenAll :1 Object.keys @exampleData
    when :time        then @covidData.times
    when :region      then @covidData.regions
    when :age         then @covidData.ages

  render: ->
    @ extract maxValue
    @graphState extract layout, numerator, denominator
    hasDenominator = denominator.length > 1 || denominator[0] != "1"
    hasNumerator = numerator.length > 1 || numerator[0] != "1"
    Element
      :column
      childrenMargins: @gridSize * 2
      animators:
        if @animationOn
          location:
            duration: 2
            f: :easeInQuad
            from: xw: -1 y: 0
        else {}

      Element
        Element
          :row :childrenBottomLeft
          animators: opacity: toFrom: 0
          childrenMargins: @gridSize
          array row in @data
            Bar row, {}
              key: row.label
              layout
              maxValue
              @unit
              colorPalette: if hasDenominator && hasNumerator then :color else :grey
              probabilityMode:
                if row.value >= 1
                  if hasDenominator then :relative
                  else :count
                else :probability

      Element
        :row
        padding: top: @gridSize * 4
        childrenMargins: @gridSize / 2

        Element
          :column :childrenTopCenter
          size: w: @gridSize * 10, hh: 1
          childrenMargins: @gridSize / 4
          array option in :graphType :numerator :denominator :region :time :age
            []
              if option == :denominator
                Element
                  size: h: 1 ww: 1
                  draw: TextPalette.black.tertiary
              else
                TextElement
                  TextStyles.tinyText
                  padding: v: @gridSize / 2
                  color: TextPalette.black.secondary
                  text: if option == :numerator then :equation else dashCase option

              Button {}
                selected = @showOptionsFor == option
                text:
                  array o in @graphState[option]
                    humanLabel o
                  .join "\n"
                  + if selected then " ▸" else " ▹"
                  .trim()
                action: ->
                  @showOptionsFor = if selected
                    null
                  else option

        Element
          @showOptionsFor && @renderLabledSection
            label: @showOptionsFor
            items: array a in order = @getOptions @showOptionsFor
              text: humanLabel a
              selected: a in @graphState[@showOptionsFor]
              action:   (event) ->
                if event.props.shiftKey
                  models.graphState.multiSelect @showOptionsFor, a, order
                else
                  models.graphState.select @showOptionsFor, a
