import &StandardImport

getEnv() extract showFullDetails

{}
  showFullDetails
  maxDisplayValue = 999999999
  minPrivValue = 1
  maxPrivValue = 9
  months = :_ :Jan :Feb :Mar :Apr :May :Jun :Jul :Aug :Sep :Oct :Nov :Dec

  sourcesUrl: :https://github.com/shanebdavis/covid-death-stats/#source-data
  repoUrl: :https://github.com/shanebdavis/covid-death-stats/

  covidDataAgeGroupsToIncludeRegEx =
    ///
      ^
      (
        All |
        Under |
        1-4 |
        \d*5- |
        85
      )


  humanLabel = (p) ->
    if m = p?.match /(^\d+-\d+) years/i
      "#{m[1]} Years"
    else if m = p?.match /^(\d\d\d\d)-(\d\d)$/
      [_, year, month] = m
      "" #{months[month|0]}, #{year}
    else
      upperCamelCase p, ' '
      .replace /(\d+$)/, " $1"
      .replace /Clrd/, "(CLRD)"

  shortHumanLabel = (p) ->
    humanLabel
      if /^All/i.test p
        p
      else
        p.replace /Pneumonia|Diseases?|Accidents?|Clrd/g, ''


  isPublicValue = (b) -> b? && (b == 0 || b > 9)
  isInfinite = (v) -> !(v < maxDisplayValue)

  nullCheck = (obj, f) ->
    if find v in obj when !v? with true
      null
    else if f is Function
      f obj
    else f

  getMaxNonInfiniteValues = (values) ->
    if values.length > 0
      m = null
      each value in values when !isInfinite value
        m = if m? then max value, m else value
      m ? Infinity

  getSumNonInfiniteValues = (values) ->
    if values.length > 0
      m = null
      each value in values when !isInfinite value
        m = if m? then m + value else value
      m ? Infinity

  fullDetails = (b) ->
    if b? then b else (maxPrivValue / minPrivValue) /2

  valuesSum = (list, canShowAnyValue) ->
    reduce a, b from list inject [0 0] with
      if showFullDetails
        []
          a[0] + if b? then b else minPrivValue
          a[1] + if b? then b else maxPrivValue

      else if canShowAnyValue
        []
          v = a[0] + b
          v

      else
        []
          a[0] + if isPublicValue b then b else minPrivValue
          a[1] + if isPublicValue b then b else maxPrivValue

  valuesMax = (list, canShowAnyValue) ->
    reduce a, b from list inject [0 0]
      if showFullDetails
        []
          max a[0], if b? then b else minPrivValue
          max a[1], if b? then b else maxPrivValue

      else if canShowAnyValue
        []
          v = max a[0], b
          v
      else
        []
          max a[0], if isPublicValue b then b else minPrivValue
          max a[1], if isPublicValue b then b else maxPrivValue

  applyScale = (scale, obj) ->
    if scale? && scale != 1
      object v, k in obj with v * scale
    else obj

  createSelectedDataEntry = (covidDataRecord, region, age, time, baselineDeathData, scale) ->
    merge
      {} region, age, time
      covidDataRecord
      nonCovid19Deaths: covidDataRecord.totalDeaths - covidDataRecord.covid19Deaths
      applyScale scale, baselineDeathData?[region]?[age] ? {}

  valueCalc = (row, numerator, denominator) ->
    n = if numerator == "1" then 1 else row[numerator]
    d = if denominator == "1" then 1 else row[denominator]
    if n? && d?
      n / d
    else null

  equationElementHasNon1Value = (values) -> !(values.length == 1 && values[0] == "1")

  hasNumerator    = ({numerator}) -> equationElementHasNon1Value numerator
  hasDenominator  = ({denominator}) -> equationElementHasNon1Value denominator

  getGraphDataElement: (rows, label, graphState) ->
    graphState extract layout, numerator, denominator, age, region

    [firstRow] = rows

    values =
      if numerator.length > 1
        valueLegendLabels = array n in numerator with shortHumanLabel n
        array n in numerator with valueCalc firstRow, n, denominator[0]
      else if denominator.length > 1
        valueLegendLabels = array l in denominator with shortHumanLabel l
        array d in denominator with valueCalc firstRow, numerator[0], d
      else
        valueLegendLabels =
          if graphState.age.length == 1 && graphState.region.length == 1
            valueLegendLabels = array n in numerator with shortHumanLabel n
          else
            array {region, age} in rows
              if graphState.age.length > 1 && graphState.region.length > 1
                "" #{upperCamelCase region, ' '}, #{age}
              else if graphState.age.length > 1
                "" #{age}
              else
                upperCamelCase region, ' '

        array row in rows with valueCalc row, numerator[0], denominator[0]

    [lowValue, highValue] =
      if layout[0] == :stacked
        valuesSum values, hasDenominator graphState
      else
        valuesMax values, hasDenominator graphState

    {}
      label:
        if m = label.match /(^\d+)-(\d+)$/
          [_, year, month] = m
          "#{months[month|0]}\n#{year}"
        else
          label
          .replace /Under /g 'Under\n'
          .replace /[ ]years/g '\nyears'

      values
      valueLegendLabels
      value: (lowValue + highValue) / 2
      valueLabel: if lowValue == highValue then highValue else "#{lowValue} - #{highValue}"
      nonInfiniteValueMax: getMaxNonInfiniteValues values
      nonInfiniteValueSum: getSumNonInfiniteValues values

  getSelectedData: ({covidData: _covidData, graphState}) ->
      _covidData extract? covidData, intentData, mechanismData, baselineDeathData, times, ages, latestYearWindowTimes, latest12MonthTimes
      graphState extract? region, time, age, graphType
      [aRegion] = region
      [aTime] = time
      [anAge] = age
      [axis] = graphType

      axisKeys = if axis == :time
        switch aTime
        when "All Time"
          array v from times when !/^(All.*|202\d|l.*)$/i.test v
        when :latestYearWindow then latestYearWindowTimes
        when :latest12Months then latest12MonthTimes
        else time
      else
        if anAge == "All Ages"
          array v from ages when !/^All/.test v
        else age

      unless covidData
        {}
      else
        if axis == :time
          object aTime from axisKeys
            if region.length > 1
              array aRegion in region
                createSelectedDataEntry
                  covidData[aRegion][aTime][anAge]
                  aRegion
                  anAge
                  aTime
                  baselineDeathData
                  1/12

            else
              array anAge in age
                createSelectedDataEntry
                  covidData[aRegion][aTime][anAge]
                  aRegion
                  anAge
                  aTime
                  baselineDeathData
                  1/12

        else
          object anAge from axisKeys
            if region.length > 1
              array aRegion in region
                createSelectedDataEntry
                  covidData[aRegion][aTime][anAge]
                  aRegion
                  anAge
                  aTime
                  baselineDeathData

            else
              array aTime in time
                createSelectedDataEntry
                  covidData[aRegion][aTime][anAge]
                  aRegion
                  anAge
                  aTime
                  baselineDeathData

  getMonthSequence = ({monthsAgo = 0, totalMonths, date = new Date()}) ->
    month = date.getMonth()
    year  = date.getYear() + 1900
    m = monthsAgo

    while m > 12
      year -= 1
      m -= 12

    month = if month < m
      year--
      m -= month
      12 - m
    else
      month - m

    array til totalMonths
      out = "#{year}-#{if month < 9 then '0' else ''}#{month + 1}"
      month++
      if month >= 12
        month = 0
        year++
      out

  loadCovidData: ->
    populateMonthSetAges = (agesByTime, times) ->
      monthsSet =
        array time from times
          {} time, ages: agesByTime[time]

      each {ages} from monthsSet into out = {}
        each stats, age in ages
          ageSum = out[age] ?= {}
          each v, stat in stats
            ageSum[stat] = if stat == :population then v else (ageSum[stat] ? 0) + v


    covidData = null
    merge
      {}
        latestYearWindowTimes = getMonthSequence {} monthsAgo: 13, totalMonths: 12
        latest12MonthTimes = getMonthSequence {} monthsAgo: 12, totalMonths: 12

      object d0, field from &covid-death-stats.getPreparedData()
        if field == :covidData
          covidData = object d1, region from d0
            list = object d2, time from d1
              object _, age from d2 when covidDataAgeGroupsToIncludeRegEx.test age

            list.latest12Months = populateMonthSetAges d1, latest12MonthTimes
            list.latestYearWindow = populateMonthSetAges d1, latestYearWindowTimes

            list

        else d0

      regions:  Object.keys covidData
      times:
        compactFlatten []
          "All Time"
          arrayWithoutValue
            Object.keys covidData.UnitedStates
            "All Time"

      ages:     Object.keys covidData.UnitedStates["2020"]
      valueFields:
        :population
        :totalDeaths
        :covid19Deaths
        :nonCovid19Deaths

        :carAccidents2019
        :drowningAccidents2019
        :poisoningAccidents2019
        :suffocationAccidents2019

        :alzheimersDisease2019
        :cancer2019
        :cerebrovascularDiseases2019
        :diabetesDisease2019
        :heartDiseases2019
        :influenzaPneumonia2019
        :liverDisease2019
        :respiratoryClrdDisease2019
        :homicides2019
        :suicides2019

        :allAccidents2019
        :allDeaths2019
        :allDiseases2019
        :allOtherDeaths2019

        :population2019
