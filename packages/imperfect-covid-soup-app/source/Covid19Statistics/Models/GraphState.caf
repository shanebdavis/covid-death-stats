import &ArtSuite

defaults =
  region:       [] :UnitedStates
  time:         [] :last12Months
  numerator:    [] :1
  denominator:  [] :1
  age:          [] "All Ages"
  graphType:    [] :Age
  layout:       [] :adjacent

class GraphState extends ApplicationState

  @stateFields merge
    defaults
    preset:       0
    numerator:    [] :covid19Deaths
    &Presets[0].graphState

  select: (type, value) ->

    @setState
      [type]: [] value

  multiSelect: (type, value, order) ->
    @setState (state) ->
      merge
        state

        # only numerator or denominator can have > 1 selected
        switch type
        when :denominator then numerator: [] state.numerator[0]
        when :numerator   then denominator: [] state.denominator[0]

        [type]:
          if value in currentSelected = state[type]
            if state[type].length > 1
              arrayWithoutValue currentSelected, value
            else defaults[type]
          else
            arrayWith currentSelected, value
          .sort (a, b) ->
            if order
              if 0 <= aIndex = order.indexOf a
                if 0 <= bIndex = order.indexOf b
                  aIndex - bIndex
                else
                  1
              else if b in order
                -1
            else
              a.localeCompare b

  setPreset: (presetNumber) ->
    @setState merge
      {} preset = presetNumber %% &Presets.length
      &Presets[preset].graphState