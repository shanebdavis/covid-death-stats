import &StandardImport

class PageBottom extends ViewStateComponent
  @subscriptions :graphState :covidData

  @stateFields showOptionsFor: null

  render: ->
    @graphState extract custom, presetData
    Element
      :row
      childrenMargins: @gridSize

      Element
        size: w: @gridSize * 12
        Legend {} @showOptionsFor, @setShowOptionsFor

      Element
        if @showOptionsFor
          GraphStatePicker
            closeAction: @clearShowOptionsFor
            label: @showOptionsFor
            items: array a in order = @covidData.graphStateOptions[@showOptionsFor]
              text: humanLabel a
              selected: a in @graphState[@showOptionsFor]
              action:   (event) ->
                if event.props.shiftKey
                  models.graphState.multiSelect @showOptionsFor, a, order
                else
                  models.graphState.select @showOptionsFor, a

        else
          Element
            key: :preset
            :row
            childrenMargins: @gridSize

            Element
              :clip
              :row :childrenTopCenter

              ScrollerWithBar2
                :centerCenter
                alwaysShowBarIfNeeded: true
                size: w: @gridSize * 48 max: ww: 1
                childrenMargins: @gridSize * 2
                padding:
                  bottom: @gridSize
                  h: @gridSize

                MarkdownView
                  text:
                    if @graphState.custom
                      """
                        # Custom

                        Explore the dataset with "edit graph"

                    else
                      presetData.description
                      .replace /(%(\w+)%)/g, (a, b, c) ->
                        commaAnd array v in @graphState[c] with humanLabel v

                if @graphState.custom
                  Button
                    text: "" Clear custom graph
                    action: models.graphState.resetPreset

            Element
              size: w: @gridSize * 12
              PageSelector {} @props.selectedData
