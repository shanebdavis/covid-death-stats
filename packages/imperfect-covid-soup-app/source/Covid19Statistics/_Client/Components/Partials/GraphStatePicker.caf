import &StandardImport

class GraphStatePicker extends ViewStateComponent

  @propFields :label :items :closeAction :size

  render: ->
    @ extract label, items

    mainContent =
      Element
        :flow
        :parentWidthChildrenHeight
        padding: 2
        key: label
        childrenMargins: @gridSize / 2
        array item in items
          Button item

    if @isPhone
      Element
        :column
        :parentWidthChildrenHeight
        childrenMargins: @gridSize

        Element
          :row
          :parentWidthChildrenHeight
          :childrenCenterLeft
          TextElement
            TextStyles.xLargeBoldText
            :childrenHeightParentWidth
            text: upperCamelCase label, ' '

          Button
            action: @closeAction
            Icon size: @reducedButtonSize, icon: :close iconScale: .5

        mainContent

    else
      Element
        :column
        padding: left: @gridSize
        childrenMargins: @gridSize

        Element
          :row
          :parentWidthChildrenHeight
          :childrenCenterLeft
          animators: opacity: toFrom: 0
          TextElement
            TextStyles.xLargeBoldText
            :childrenHeightParentWidth
            text: upperCamelCase label, ' '

          Button
            action: @closeAction
            Icon size: @reducedButtonSize, icon: :close iconScale: .5

        ScrollerWithBar2
          alwaysShowBarIfNeeded: true
          draw:
            padding:
              h: -@gridSize
              top: -@gridSize
            :clip

          mainContent


        !@isPhone && TextElement
          TextStyles.tinyText
          color: TextPalette.black.secondary
          text: "" shift-click to pick more than one