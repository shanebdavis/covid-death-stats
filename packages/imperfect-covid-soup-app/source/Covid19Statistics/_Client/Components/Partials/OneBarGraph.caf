import &StandardImport, &Widgets, {} &Bar

class OneBarGraph extends ViewStateComponent
  @propFields
    :data
    :graphType     # :stacked :adjacent
    :unit       # string
    :inColor    # boolean

  render: ->
    total = 0
    minValue = undefined
    maxValue = undefined
    @ extract data
    values = array row, i in data

      total += v = reduce sum, v from row.values inject 0 with sum + v
      minValue = min v, minValue ? v
      maxValue = max v, maxValue ? v
      v

    valueRange = maxValue - minValue

    sorted =
      array v, i in values
        [] v, i
      .sort (a, b) ->
        b[0] - a[0]
    sortedIndexes = array [v, i] in sorted with i
    values = array i in sortedIndexes with values[i]
    data = array i in sortedIndexes with data[i]

    palette = barColors.color
    c1 = palette[0]
    c2 = c1.blend :black, .5

    Element
      :column
      # draw: :red
      Element
        size: hh: .25
      Element
        :row
        size: hh: .25
        array row, i in data
          Element
            # animators: :size :draw
            size: ww: value = values[i]
            draw:
              if /covid/.test row.label
                #d00
              else
                c1.blend c2, i / (data.length - 1)
              outline:
                color:      #000
                lineWidth:  3

      Element
        size: h: @gridSize * 5
        draw:
          {} shape: (context, {w, h}) ->
            xFrom = 0
            xTo = 0.5 * xToWidth = w / values.length

            context.beginPath()
            each value in values
              xFromWidth = w * value / total
              xFromNow = xFrom + xFromWidth * .5
              context.moveTo xFromNow, 0
              # context.lineTo xFromNow, h * .1
              context.lineTo xTo, h * .9
              context.lineTo xTo, h
              xFrom += xFromWidth
              xTo += xToWidth

          outline:
            color: TextPalette.black.disabled
            lineWidth: 2

      Element
        :row
        size: hh: .25
        padding: top: @gridSize
        array row, i in data
          Element
            :column
            childrenMargins: @gridSize
            TextElement
              smallBoldText
              :textTopCenter
              text: formatPercent values[i] / total
            TextElement
              smallText
              :textTopCenter
              text: row.label
            TextElement
              :textTopCenter
              tinyText
              color: TextPalette.black.secondary
              text: "" (#{formatNumber values[i]})

