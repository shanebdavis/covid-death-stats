import &StandardImport

nullCheck = (obj, f) ->
  if find v in obj when !v? with true
    null
  else if f is Function
    f obj
  else f

formatRelative = (p) -> p.toFixed if p < 1 then 3 else if p < 10 then 2 else 1
formatOdds = (p) -> "" 1:#{} commaize round(1/p) | 0
formatProbability = (p) -> "#{(100*p).toFixed 4}%"

buildCodCompareFunction = (field) ->
  argument: "" [one or more specific #{field} fields to show - default: all]
  description:
    ""
      Absolute probability of death due to non-covid causes of death by #{capitalize field} (2019 data).
      Combine with `--relatve` option to compare how much more likely each non-covid cause is compared to covid.
      Combine with `--odds` to display the absolute odds of each cause of death.

  f: (stats, options) ->
    codOption = options[field]
    {odds, relative, relativeOdds} = options
    codData = stats[field]
    statsWeCareAbout = {covid19Deaths, population} = stats
    if codData
      covidDeathProbability = covid19Deaths / population
      codPopulation = codData.population

      m =
        object v, k in codData when k != :population && k != :totalDeaths
          deathProbability = v / codPopulation
          if relative
            formatRelative deathProbability / covidDeathProbability

          else if odds
            formatOdds deathProbability

          else formatProbability deathProbability

      if codOption?.length > 0
        m = object key from compactFlattenAll codOption with m[key]

      nullCheck
        statsWeCareAbout
        m

totalDeathRisk:
  f: (stats, options) ->
    nullCheck
      if options.relative then {population, covid19Deaths, totalDeaths} = stats
      else {population, totalDeaths} = stats

      if options.odds
        formatOdds totalDeaths / population
      else if options.relative
        formatRelative totalDeaths / covid19Deaths
      else
        formatProbability totalDeaths / population

covidDeathOdds:
  description: "" The odds of dying dying of Covid-19. For example, 1:100 is less chance of dying than 1:10. Higher is better.
  f: (stats) ->
    nullCheck
      {population, covid19Deaths} = stats
      ->
        if covid19Deaths == 0 then "0:999,999,999" else "" 1:#{} commaize round(population/covid19Deaths) | 0

totalDeathOdds:
  description: "" The odds of dying of anything. For example, 1:100 is less chance of dying than 1:10. Higher is better.
  f: (stats) -> nullCheck
    {population, totalDeaths} = stats
    if totalDeaths > 0
      "" 1:#{} commaize round(population/totalDeaths) | 0
    else null

absoluteCovidDeathRisk:
  description: "" Percentage chance of death by Covid-19. This is also the ABSOLUTE increased risk of death. Lower is better.
  f: (stats) -> nullCheck
    {covid19Deaths, population} = stats
    formatProbability covid19Deaths / population

absoluteTotalDeathRisk:
  description: "" Percentage chance of death. This is also the ABSOLUTE increased risk of death. Lower is better.
  f: (stats) -> nullCheck
    {population, totalDeaths} = stats
    formatProbability totalDeaths / population

increasedRiskDueToCovid:
  description:  "" Increased disk of death due to covid-19 as a percentage. This is the RELATIVE increased risk of death over all other kinds of death. Lower is better.
  f: (stats) -> nullCheck
    {totalDeaths, covid19Deaths} = stats
    if totalDeaths > covid19Deaths
      "" #{(100 * covid19Deaths / (totalDeaths - covid19Deaths)).toFixed 2}%
    else :0.00%

relativeOddsOfCovidDeath:
  description: "" If you died, the odds you died of covid-19. For example, an odds of 1:20 means you are 20 times more likely to die of something else other than covid-19. Higher is better.
  f: (stats) -> nullCheck
    {totalDeaths, covid19Deaths} = stats
    if covid19Deaths == 0 then null else "" 1:#{} commaize min round(totalDeaths / covid19Deaths)

relativeNonCovidDeathProbability:
  description: "" If you died, the probability that it was something other than covid compared to the proability it was covid. i.e. "5 times more likely to die of XYZ than covid"
  f: (stats) -> nullCheck
    {totalDeaths, covid19Deaths} = stats
    nonCovidDeaths = totalDeaths - covid19Deaths
    / covid19Deaths
    .toFixed 1

intent:
  buildCodCompareFunction :intent

mechanism:
  buildCodCompareFunction :mechanism

## "expectedYearsBeforeMoreThan#{round(100 * expectedYearsPercent)|0}PercentChanceOfCovidDeath":
expectedYears:
  argument: "[1% to 100% - default: 50%]"
  default: "50%"
  description:
    ""
      The expected number of years before the death-risk due to covid-19 exceeds the provided percentage.
      This assumes the chance of covid-19 dead the is same year after year.
      Higher is better.

  f: (stats, {expectedYearsPercent, time}) -> nullCheck
    {totalDeaths, covid19Deaths, population} = stats
    ->
      absoluteCovidDeathRisk = covid19Deaths / population
      yearsSurvivalDivisor = if /^\d+$/.test(time) then 1 else 12
      if covid19Deaths == 0
        null
      else
        commaize Math.floor (Math.log(1 - expectedYearsPercent) / Math.log(1 - absoluteCovidDeathRisk)) / yearsSurvivalDivisor
